apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "com.flyaudio.soundeffect"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        flavorDimensions "versionCode"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        def KEY_ALIAS = "sign"
        def KEY_PSWD = "123456"
        def STORE_FILE = "../signature/sign_8257.jks"
        def STORE_FILE_PSWD = "123456"
        if (project.hasProperty('FA_SIGN_KEY')) {
            KEY_ALIAS = FA_SIGN_KEY
        }
        if (project.hasProperty('FA_SIGN_KEY_PSWD')) {
            KEY_PSWD = FA_SIGN_KEY_PSWD
        }
        if (project.hasProperty('FA_STORE_FILE')) {
            STORE_FILE = FA_STORE_FILE
        }
        if (project.hasProperty('FA_STORE_PASSWORD')) {
            STORE_FILE_PSWD = FA_STORE_PASSWORD
        }

        debug {
            keyAlias KEY_ALIAS
            keyPassword KEY_PSWD
            storeFile file(STORE_FILE)
            storePassword STORE_FILE_PSWD
        }

        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PSWD
            storeFile file(STORE_FILE)
            storePassword STORE_FILE_PSWD
        }
    }

    repositories {
        flatDir {
            dirs 'sys-libs', 'flyaudio-libs', 'libs'
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "${APP_NAME}_" + buildType.name + "_v${defaultConfig.versionName}_${releaseTime()}.apk"
            }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res/comm',
                    'src/main/res/main',
                    'src/main/res/config'
            ]
        }
    }
}

dependencies {

    // android support
    implementation "com.android.support:appcompat-v7:$appcompat_v7_version"
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "com.android.support.test:runner:$runner_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_core_version"

    // recycler-view
    implementation "com.android.support:recyclerview-v7:$recyclerview_version"

    // flyaudio-libs
    implementation(name: 'flyaudio-base', ext: 'aar')
    implementation(name: 'flyaudio-res', ext: 'aar')
    implementation(name: 'flyaudio-ui', ext: 'aar')
    implementation(name: 'flyaudio-service-dsp-client', ext: 'aar')
    implementation(name: 'flyaudio-service-comm-base', ext: 'aar')
    implementation(name: 'flyaudio-service-comm-utils', ext: 'aar')
    implementation(name: 'flyaudio-service-dsp-aidl', ext: 'aar')

    // gson
    implementation "com.google.code.gson:gson:$gson_version"
    // rxjava
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroid_version"
    // leak-canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

}

static def releaseTime() {
    return new Date().format("yyMMddHHmmss")
}

ext {
    APP_NAME = "SoundEffect"
}
